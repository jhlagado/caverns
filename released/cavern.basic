20 CLS :H=11:D=128:W=0:G=0:T=0:E=0: DIM I(3):J=0: RESTORE 1880: DIM P(24): FOR X=1 TO 24: READ P(X): NEXT X:R=0:A=1:C0=1
30 CURS 14,5: SPEED 255: PRINT "C A V E R N S      by John Hardy (c) 1983"\\ : SPEED 0
40 CURS 14,7: PRINT "Released under GNU Public License in 2019": CURS 0: PLAY 0,40: CLS :F=0:U=0
50 CURS 1,7: PRINT [A63 45]
60 IF Z>0 AND Z<>5 AND M<>20 THEN 1110
70 IF A<18 OR C0=1 AND (P(21)=A OR P(21)=-1) THEN 100
80 PRINT "It's very dark, too dark to see anything...I'm scared!"
90 GOTO 720
100 IF A=1 THEN PRINT  "You are standing in a darkened room. There is a door to the     north."
110 IF A=2 THEN PRINT  "You are in a forest clearing before a small bark hut. There     are no windows, and locked door to the south. The latch was engaged when you closed the door."
120 IF A=3 THEN PRINT  "You are deep in a dark forest. In the distance you can see      a mighty river."
130 IF A=4 THEN PRINT  "You are standing in a field of four-leafed clovers. There       is a small hut to the north."
140 IF A=13 THEN PRINT "You are standing on a rock in the middle of a mighty oak        forest.Surrounding you are thousands of poisonous mushrooms."
150 IF A=15 THEN PRINT "You are on a cliff face over looking the river."
160 IF A=5 THEN PRINT  "The forest has opened up at this point. You are standing on     a cliff overlooking a wide glacial river. A small foot-beaten pathleads south."
170 IF A=6 THEN PRINT  "You are standing at the rocky edge of the mighty river          Gioll. The path forks east and west."
180 IF A=7 THEN PRINT  "You are on the edge of an enormous crater. The rim is           extremely slippery. Clouds of water vapour rise high in the air as the Gioll pours into it."
190 IF A=8 THEN PRINT  "The path to the east stops here. You are on a rocky             outcrop, projected about 15 feet above the river. In the distance, a tiny bridge spans the river."
200 IF A=9 THEN PRINT  "You are on the lower slopes of Mt. Ymir. The forest             stretches far away and to the west. Arctic winds blow fiercely, it's very cold!"
210 IF A=10 THEN PRINT "You stand on a rocky precipice high above the river,            Gioll; Mt. Ymir stands to the north. A flimsy string bridge spans the mighty river."
220 IF A=11 THEN PRINT "You have made your way half way across the creaking             bridge. It sways violently from side to side. It's going to collapse anysecond!!"
230 IF A=12 THEN PRINT "You are on the southern edge of the mighty river, before        the string bridge."
240 IF A=14 THEN PRINT "You are in a clearing in the forest. An ancient basalt rock     formation towers above you. To your south is the entrance of a VERY interesting cave..."
250 IF A=16 THEN PRINT "You are just inside the cave. Sunlight pours into the cave      lighting a path to the east and another to the south. I don't mind saying I'm a bit scared!"
260 IF A=17 THEN PRINT "This passage appears to be a dead end. On a wall before you     is carved `Find the Sacred Key of Thialfi'."
270 IF A=19 THEN PRINT "You are in the legendary treasure room of the black elves       of Svartalfheim. Every red-blooded Viking has dreamed of entering this sacred room."
280 IF A=20 THEN PRINT "You can see a small oak door to the east. It has been           locked from the inside."
290 IF A=22 THEN PRINT "You are standing in an east-west corridor. You can feel         a faint breeze coming from the east."
300 IF A=23 THEN PRINT "You are standing in what appears to have once been a            torture chamber. Apart from the rather comprehensive range of instumentsof absolutely inhuman agony,"
310 IF A=23 THEN PRINT "coagulated blood stains on the walls and mangled bits of        bone onthe floor make me think that a number of would be adventurers croaked it here!"
320 IF A=24 THEN PRINT "You stand in a long tunnel which has been bored out of the      rock. It runs from north to south. A faint glow comes from a narrow crack in the eastern wall."
330 IF A=26 THEN PRINT "You are in a large round room with a number of exits. The       walls have been painted in a mystical dark purple and a big chalk staris drawn in the centre of "
340 IF A=26 THEN PRINT "the floor. Note: This is one of the hidden chambers of the      infamous pagan sect, the monks of Loki. Norse folk believe them to be gods."
350 IF A=27 THEN PRINT "You are standing on a narrow ledge, high above a                subterranean river. There is an exit to the east. "
360 IF A=28 THEN PRINT "You are on a balcony, overlooking a huge cavern which has       been converted into a pagan temple. Note: this temple has been dedicated to Loki, the god of"
370 IF A=28 THEN PRINT "fire, who came to live in Svartalfheim after he had been        banished to exile by Odin. Since then he has been waiting for the `End Of All Things'."
380 IF A=33 THEN PRINT "You are in the central cave of a giant bat colony. Above        you hundreds of giant bats hang from the ceiling and the floor is covered in centuries of "
390 IF A=33 THEN PRINT "giant bat droppings. Careful where you step! Incidentally,      the smell is indescribable."
400 IF A=35 THEN PRINT "You are in the temple. To the north is a locked gate and on     the wall is a giant statue of Loki, carved out of the living rock itself!"
410 IF A=37 THEN PRINT "You stand in an old and musty crypt, the final resting          place of hundreds of Loki devotees. On the wall is carved:``What 3 letter word completes a word "
420 IF A=37 THEN PRINT "starting with 'G---' and another ending with '---X'' Note:      The monks of Loki must have liked silly puzzles. Putrefaction and decay fills the air here."
430 IF A=38 THEN PRINT "You are in a tiny cell. The western wall has now firmly         closed again. There is a ventilator shaft on the eastern wall."
440 IF A=40 THEN PRINT "You are on another ledge high above a subterranean river.       The water flows in through a hole in the cavern roof, to the north."
450 IF A>40 AND A<45 THEN PRINT "Somehow you have gotten into the complex drainage      system of thisentire cavern network!!"
460 IF A=45 THEN PRINT "You are standing near an enormous waterfall which brings        water down from the surface, from the river Gioll."
470 IF A=47 THEN PRINT "You are standing before a stone staircase which leads           southwards."
480 IF A=48 THEN PRINT "You are on a narrow and crumbling ledge. On the other side      of the river you can see a magic castle. (Don't ask me why it's magic...I just know it is)"
490 IF A=49 THEN PRINT "You are by the drawbridge which has just lowered itself....     by magic!!"
500 IF A=50 THEN PRINT "You are in the courtyard of the magic castle. WOW! This         castle is really something! On the wall is inscribed 'hzb tzozi'. A secret escape tunnel leads so
510 IF A=51 THEN PRINT "You are in the powder magazine of this really super             castle."
520 IF A=53 THEN PRINT "You stand before a small wooden bridge which crosses the        river."
530 IF A=52 THEN PRINT "You are on the eastern side of the river. A small tunnel        leads east into the cliff face."
540 IF A=54 THEN PRINT "You are in a conduit draining into the river. The water         comes upto your knees and is freezing cold. A narrow service path leads south."
550 IF A=18 OR A=21 OR A=25 OR A>28 AND A<32 OR A=34 OR A=36 OR A=39 OR A=46 OR A=53 THEN PRINT "You are deep in a dark cavern."
560 IF (A=10 OR A=12) AND H=128 THEN PRINT "Two of the ropes have snapped under         your weight. It's totally unfit to cross again."
570 IF A=14 AND P(4)=0 THEN PRINT "You can also see the bloody corpse of an             enormous dragon."
580 IF A=48 AND D=49 THEN PRINT " A mighty golden drawbridge spans the waters."
590 IF U>200 THEN PRINT "Your candle is growing dim."
600 IF U<230 THEN 630
610 C0=0
620 PRINT "In fact...it went out!"
630 V=0
640 FOR L=7 TO 24
650 IF P(L)=A THEN LET V=V+1
660 NEXT L
670 IF V=0 THEN 720
680 PRINT "You can also see..."
690 FOR L=7 TO 24
700 IF P(L)=A THEN GOSUB 1650
710 NEXT L
720 V=0: FOR L=1 TO 6: IF P(L)=A THEN LET V=V+1: GOSUB 2070
730 NEXT L: IF V=0 THEN 770
740 PRINT "Nearby there lurks..."
750 FOR L=1 TO 6: IF P(L)=A THEN GOSUB 1650
760 NEXT L
770 PRINT :R=1: PRMT ( )
780 IF Z>0 AND Z<>5 AND M<>20 THEN 1110
790 INPUT A0$: IF A0$="" THEN 790 ELSE LET A0$=" "+A0$+" ":U=U+1
800 FOR X=1 TO LEN (A0$):Y= ASC (A0$(;X)): IF Y>64 AND Y<91 THEN LET A1$=A0$(;1,X-1)+ CHR (Y+32)+A0$(;X+1):A0$=A1$
810 NEXT X: CLS
820 RESTORE 1840
830 FOR M=7 TO 24: READ N0$,N1$
840 IF SEARCH (A0$,N1$)>0 THEN NEXT *M 860
850 NEXT M:N0$="":N1$="":M=0
860 IF A=11 THEN LET H=128
870 IF A=2 THEN LET J=1
880 IF A=45 THEN LET W=43
890 IF A=35 THEN LET W=0
900 IF P(24)<>38 THEN LET G=39
910 IF A=49 THEN LET D=49
920 IF SEARCH (A0$," look ")>0 THEN LET R=0: GOTO 50
930 IF SEARCH (A0$," list ")=0 THEN 1010
940 PRINT "You are carrying ";:V=0
950 FOR L=7 TO 24
960 IF P(L)=-1 THEN LET V=V+1
970 NEXT L
980 IF V=0 THEN PRINT "nothing.": GOTO 50
990 PRINT : FOR L=7 TO 24: IF P(L)=-1 THEN GOSUB 1650
1000 NEXT L: GOTO 50
1010 IF SEARCH (A0$," quit ")=0 THEN 1070
1020 S=0: FOR X=7 TO 17: IF P(X)=-1 THEN LET S=S+X-6
1030 IF P(X)=1 THEN LET S=S+(X-6)*2
1040 NEXT X
1050 PRINT \\"You have a score of";S;" out of a possible 126 points in";U;" moves.": GOSUB 1920: PRINT "Another adventure? ";:Y0$= KEY $
1060 Y0$= KEY $: IF Y0$="N" OR Y0$="n" THEN 2090 ELSE IF Y0$="Y" OR Y0$="y" THEN RUN ELSE 1060
1070 FOR Z=1 TO 6: IF P(Z)=A THEN NEXT *Z 1090
1080 NEXT Z:Z=0: GOTO 1660
1090 IF Z=5 THEN PRINT "The giant bat picked you up and carried you to another place.":A=33:R=0:P(5)=P(5)+7: GOTO 50
1100 GOTO 1660
1110 RESTORE 1820: FOR N=1 TO Z: READ K0$,K1$: NEXT N
1120 PRINT "AUUUUUGH...you've just been killed by a";K0$;K1$;"!!": GOTO 1020
1130 RESTORE 1890: FOR Q=0 TO 3: READ M0$: IF SEARCH (A0$,M0$)>0 THEN NEXT *Q 1150
1140 NEXT Q: GOTO 1240
1150 IF P(8)=-1 OR P(8)=A THEN 1170
1160 Q= INT ( RND *4)
1170 RESTORE 1740: FOR N=1 TO A
1180 FOR O=0 TO 3: READ I(O): NEXT O: NEXT N
1190 B=I(Q)
1200 IF B=0 THEN PRINT "You can't go that way"
1210 IF B=128 THEN PRINT "You stumble and fall into the chasm and smash yourself to a pulp on the rocks below.": GOTO 1020
1220 IF B>0 THEN LET A=B
1230 R=0: GOTO 50
1240 IF SEARCH (A0$," galar ")=0 THEN 1260
1250 R=0: PRINT "Suddenly a magic wind carried you to another place...":A=16: GOTO 50
1260 IF SEARCH (A0$," ape ")=0 THEN 1280
1270 PRINT "Hey! the eastern wall of the crypt slid open...":E=38: GOTO 50
1280 IF M<1 THEN PRINT "eh?": GOTO 50
1290 IF P(M)=-1 OR P(M)=A THEN 1300 ELSE PRINT "Where? I can't see it.": GOTO 50
1300 IF SEARCH (A0$," get ")=0 THEN 1370
1310 Q=0
1320 FOR X=7 TO 24
1330 IF P(X)=-1 THEN LET Q=Q+1
1340 NEXT X
1350 IF Q>10 THEN PRINT "You are carrying too many objects.": GOTO 50
1360 P(M)=-1: GOTO 50
1370 IF SEARCH (A0$," drop ")=0 THEN 1390
1380 LET P(M)=A: GOTO 50
1390 ON M-18 GOTO 1410,1430,1570,1630
1400 PRINT "How am I supposed to use it?": GOTO 50
1410 IF A<>2 AND A<>35 THEN PRINT "It won't open!": GOTO 50
1420 PRINT "You opened the door.":P(19)=A:R=0: IF A=2 THEN LET A=1: GOTO 50 ELSE LET A=37: GOTO 50
1430 IF Z=0 THEN PRINT "But there's nothing to kill...": GOTO 50
1440 F=F+1: IF RND *7+15> FLT (F) THEN 1460
1450 PRINT "You swing with your sword but miss and the creature smashes your skull.": GOTO 1020
1460 IF RND <.38 THEN 1530
1470 L= INT ( RND *4): IF Z=5 THEN 1090
1480 IF L=0 THEN PRINT "You attack but the creature moves aside."
1490 IF L=1 THEN PRINT "The creature deflects your blow."
1500 IF L=2 THEN PRINT "The foe is stunned but quickly regains his balance."
1510 IF L=3 THEN PRINT "You missed and he deals a blow to your head."
1520 GOTO 50
1530 PRINT "The sword strikes home and your foe dies...":
1535 P(M)=-1: IF Z=3 OR Z=5 THEN LET P(Z)=P(Z)+10 ELSE LET P(Z)=0: IF Z=1 THEN PRINT "Hey! Your sword has just crumbled into dust."
1540 IF Z<>4 THEN PRINT \"Suddenly a black cloud descends and the corpse vaporizes into nothing."
1550 Z=0
1560 GOTO 50
1570 IF P(9)=-1 OR P(9)=A THEN 1590
1580 PRINT "That won't burn, Dummy...In fact, the candle went out.":C0=0: GOTO 50
1590 IF C0=1 THEN 1610
1600 PRINT "But the candle is out, stupid!!": GOTO 50
1610 PRINT "The fuse burnt away and....BOOM!!....the explosion blew you out of the way (Lucky!)":R=0: IF A>1 THEN LET A=A-1: IF A=20 THEN LET T=19
1620 P(9)=0: GOTO 50
1630 IF A=28 THEN 1640 ELSE PRINT "It's too dangerous!!!": GOTO 50
1640 PRINT "You descend the rope, but it drops 10 feet short of the floor. You jump the rest of the way.":R=0:P(M)=A:A=35: GOTO 50
1650 RESTORE 1820: FOR K=1 TO L: READ H0$,H1$: NEXT K: PRINT "a";H0$;H1$;", ";: RETURN
1660 RESTORE 1900: FOR O=1 TO 16: READ P0$: IF SEARCH (A0$,P0$)>0 THEN NEXT *O 1690
1670 NEXT O
1680 GOTO 1130
1690 ON O GOTO 1310,1380,1390,1390
1700 IF O<7 THEN PRINT "Nothing happens!"
1710 IF O>6 AND O<13 THEN PRINT "Please tell me how."
1720 IF O>12 THEN PRINT "I can't!"
1730 PRINT : GOTO 790
1740 DATA 2,0,0,0,0,0,3,4,2,5,5,0,2,5,0,9,0,6,3,4,5,0,7,8,0,0,128,6,0,0,6,0,0,10,4,0,9
1750 DATA H,4,0,10,12,128,128,H,13,13,0,12,12,14,12,15,16,0,13,0,14,0,0,14,18,0,17,0
1760 DATA 0,16,0,16,0,23,0,0,0,20,0,21,23,0,T,0,24,0,20,0,23,21,16,22,0,18,18,21,26,0
1770 DATA 18,0,27,24,0,24,27,29,25,18,0,0,28,0,0,27,0,0,33,0,26,29,31,0,0,32,0,0,0
1780 DATA 33,30,0,0,0,31,34,0,0,0,0,33,0,0,0,0,39,0,35,40,0,35,0,E,W,0,0,G,0,36,38,0
1790 DATA 45,48,36,128,46,43,54,42,46,43,41,43,46,38,42,44,47,47,0,47,0,40,0,128
1800 DATA 47,0,47,47,0,45,46,0,40,128,0,D,0,0,48,50,0,52,49,51,0,0,50,0,50,0,53,50
1810 DATA 54,0,0,52,0,53,41,0
1820 DATA "n evil"," wizard "," fiery"," demon ","n axe wielding"," troll "," fire breathing"," dragon "," giant"," bat "
1830 DATA "n old and gnarled"," dwarf "
1840 DATA " gold"," coin "," useful looking"," compass "," home made"," bomb "," blood red"," ruby "," sparkling"," diamond "
1850 DATA " moon-like"," pearl ","n interesting"," stone "," diamond studded"," ring "," magic"," pendant "," most holy"," grail "," mirror like"," shield "
1860 DATA " nondescript black"," box ","n old an rusty"," key "," double bladed"," sword "," small"," candle "," thin and tatty"," rope "," red house"," brick "," rusty ventilation"
1870 DATA " grill "
1880 DATA 36,19,10,14,17,47,8,1,51,45,22,46,54,19,19,19,19,0,34,7,18,15,24,38
1890 DATA " north "," south "," west "," east "
1900 DATA " take "," put "," using "," with "," cut "," break "," unlock "," open "," kill "," attack "
1910 DATA " light "," burn "," up "," down "," jump "," swim "
1920 PRINT "This gives you an adventurer's ranking of:"
1930 IF S<20 THEN PRINT "Hopeless beginner"
1940 IF S<50 AND S>19 THEN PRINT "Experienced loser"
1950 IF S<100 AND S>49 THEN PRINT "Average Viking"
1960 IF S<126 AND S>99 THEN PRINT "Excellent...but you've left something behind!"
1970 IF S=126 THEN PRINT "Perfectionist and genius!!"
1980 RETURN
1990 INPUT A0$: CLS : RETURN
2000 PRINT "There, before you in a swirling mist stands an evil wizard with his hand held outwards...`Thou shall not pass' he cries."
2010 GOTO 770
2020 PRINT "Before the entrance of the cave lies an enormous, green, sleeping dragon. Realizing your presence, its eyes flicker open"
2030 PRINT "and it leaps up, breathing jets fire at you."
2040 GOTO 770
2050 PRINT "From around the corner trots an old and gnarled drawf carrying a lantern. `My job is to protect these stone steps!' he says andlunges at you with his dagger."
2060 GOTO 770
2070 IF L=1 THEN 2000 ELSE IF L=4 THEN 2020 ELSE IF L=6 THEN 2050 ELSE RETURN
